group 'org.urllib'
version '0.3.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'ru.vyarus:gradle-animalsniffer-plugin:1.4.2'
    }
}

apply plugin: 'ru.vyarus.animalsniffer'
apply plugin: 'java-library'
apply plugin: 'osgi'
apply from: 'maven-central.gradle'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

configurations {
    apt
}

sourceCompatibility = JAVA_VERSION
targetCompatibility = JAVA_VERSION

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'com.google.auto.value:auto-value:1.5.1'
    apt 'com.google.auto.value:auto-value:1.5.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.guava:guava:23.1-jre'

    signature 'org.codehaus.mojo.signature:java17:1.0@signature'
    signature 'org.codehaus.mojo.signature:java18:1.0@signature'
    signature 'net.sf.androidscents.signature:android-api-level-14:4.0_r4@signature'
}

jar {
    manifest {
        instruction 'Bundle-Vendor', 'urllib Contributors'
        instruction 'Bundle-DocURL', 'https://github.com/EricEdens/urllib'
        instruction 'Automatic-Module-Name', 'org.urllib'
    }
}

compileJava {
    options.annotationProcessorPath = configurations.apt
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PUBLIC
    exclude 'org/urllib/internal/*'
}